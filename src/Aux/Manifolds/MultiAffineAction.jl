"""
    MultiAffineAction(
      group::MultiAffine,
      selector::AbstractVector,
      conv::ActionDirection=LeftAction()
      )

Given a fixed vector ``S`` of size ``k`` (the `selector`),
this defines an action of the element ``[X;R]`` of the [`MultiAffine`](@ref) group (so ``X`` is a ``n√ók`` matrix and ``R`` is an element of a matrix group)
 on the vector ``p`` of size ``n``.
The action is defined by ``[X;R]‚ãÖp := XS+Rp``.
"""
struct MultiAffineAction{TH,dim,size,ùîΩ,TAD<:ActionDirection,TS<:AbstractVector} <: AbstractGroupAction{TAD}
    group::MultiAffine{TH,dim,size,ùîΩ}
    selector::TS # vector of length `size`
end

Base.show(io::IO, A::MultiAffineAction{<:Any,<:Any,<:Any,<:Any,TAD}) where {TAD} = print(io, "MultiAffineAction($(A.group), $(A.selector), $TAD())")

function MultiAffineAction(
    group::MultiAffine{TH, dim, size, ùîΩ},
    selector,
    conv::ActionDirection=LeftAction()
    ) where {TH, dim, size, ùîΩ}
    @assert Base.size(selector, 1) == size
    return MultiAffineAction{TH, dim, size, ùîΩ, typeof(conv), typeof(selector)}(group, selector)
end

"""
In the case k=1, this is the standard affine action.
"""
function MultiAffineAction(
    group::MultiAffine{TH, dim, 1, ùîΩ},
    conv::ActionDirection=LeftAction()
    ) where {TH, dim, ùîΩ}
    return MultiAffineAction(group, [1], conv)
end

function Manifolds.switch_direction(A::MultiAffineAction{TH,dim,size,ùîΩ,TAD}) where {TH,dim,size,ùîΩ,TAD}
    return MultiAffineAction(A.group, A.selector, switch_direction(TAD()))
end


Manifolds.base_group(A::MultiAffineAction) = A.group
Manifolds.group_manifold(::MultiAffineAction{G,dim,size,ùîΩ}) where {G,dim,size,ùîΩ} = Euclidean(dim; field=ùîΩ)

get_selector(A::MultiAffineAction) = A.selector

Manifolds.apply(::MultiAffineAction{TH,dim,size,ùîΩ,conv},
                ::Identity{MultiAffineOp{TH,dim,size,ùîΩ}}, p) where {TH,dim,size,ùîΩ,conv} = p

function Manifolds.apply!(
    A::MultiAffineAction{TH,dim,size,ùîΩ,LeftAction},
    q,
    œá,
    p, # vector of size dim
    ) where {TH,dim,size,ùîΩ}
    G = base_group(A)
    M,R = submanifold_components(G, œá)
    sel = get_selector(A)
    # compute: M*sel + R*p
    LinearAlgebra.mul!(q, M, sel)
    LinearAlgebra.mul!(q, R, p, 1, 1)
    return q
end


Manifolds.apply(A::MultiAffineAction{TH,dim,size,ùîΩ,RightAction}, a, p) where {TH,dim,size,ùîΩ} = apply(switch_direction(A), inv(base_group(A), a), p)



function Manifolds.apply_diff_group(
    A::MultiAffineAction{TH,dim,size,ùîΩ,LeftAction},
    ::Identity{MultiAffineOp{TH,dim,size,ùîΩ}},
    Œæ,
    p
    ) where {TH,dim,size,ùîΩ}
    G = base_group(A)
    Œº, œâ = submanifold_components(G, Œæ)
    sel = get_selector(A)
    @assert is_point(group_manifold(A), p)
    return Œº*sel + œâ*p
end


function Manifolds.apply_diff_group(
    A::MultiAffineAction{TH,dim,size,ùîΩ,RightAction},
    I::Identity{MultiAffineOp{TH,dim,size,ùîΩ}},
    Œæ,
    p
    ) where {TH,dim,size,ùîΩ}
    return apply_diff_group(switch_direction(A), I, -Œæ, p)
end
